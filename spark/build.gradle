/*
Licensed to the Apache Software Foundation (ASF) under one
or more contributor license agreements.  See the NOTICE file
distributed with this work for additional information
regarding copyright ownership.  The ASF licenses this file
to you under the Apache License, Version 2.0 (the
"License"); you may not use this file except in compliance
with the License.  You may obtain a copy of the License at

  http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing,
software distributed under the License is distributed on an
"AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
KIND, either express or implied.  See the License for the
specific language governing permissions and limitations
under the License.
*/
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'jacoco'

sourceCompatibility = '11'
targetCompatibility = '11'

group = 'com.ibm.research.drl.dpt'
version = '6.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        url = uri('https://www.dcm4che.org/maven2')
    }
}

ext {
    junit_jupiter_version = '5.10.0'
    junit_platform_launcher_version = '1.10.1'

    mockito_version = '5.4.0'
    jdefault_version = '2.0.0'
    hamcrest_version = '2.2'

    jackson_version = '2.15.3'
    jackson_dataformat_version = '2.15.3'

    log4j2_version = '2.19.0'

    commons_codec_version = '1.13'
    commons_csv_version = '1.8'
    commons_lang_version = '3.10'
    commons_math_version = '3.6.1'
    commons_text_version = '1.14.0'
    commons_validator_version = '1.7'
    commons_cli_version = "1.11.0"

    scala_version = '2.13'
    spark_version = '3.2.3'

    poi_version = "5.2.2"
}

dependencies {
    implementation "${group}:data-privacy-toolkit:${version}"

    implementation "com.fasterxml.jackson.core:jackson-annotations:${jackson_version}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jackson_version}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jackson_version}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jackson_dataformat_version}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:${jackson_dataformat_version}"
    implementation 'org.lz4:lz4-java:1.8.1'

    implementation "commons-cli:commons-cli:${commons_cli_version}"
    implementation "org.apache.commons:commons-csv:${commons_csv_version}"

    implementation "org.apache.spark:spark-core_${scala_version}:${spark_version}"
    implementation "org.apache.spark:spark-sql_${scala_version}:${spark_version}"

    testImplementation "org.apache.commons:commons-text:${commons_text_version}"
    testImplementation "org.apache.logging.log4j:log4j-core:${log4j2_version}"
    testImplementation "org.junit.jupiter:junit-jupiter:${junit_jupiter_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_jupiter_version}"
    testImplementation "org.mockito:mockito-core:${mockito_version}"
    testImplementation "org.hamcrest:hamcrest:${hamcrest_version}"
    testImplementation "org.beer30:jdefault:${jdefault_version}"

    testRuntimeOnly "org.junit.platform:junit-platform-launcher:${junit_platform_launcher_version}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit_jupiter_version}"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
    minHeapSize = "5g"
    maxHeapSize = "10g"

    //jvmArgs '--add-exports java.base/sun.nio.ch=ALL-UNNAMED'

    testLogging {
        events 'failed'
        exceptionFormat 'short'

        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams false
    }
}

tasks.create(name: "testCoverage", type: JacocoReport, dependsOn: "test") {

    group = "Reporting"
    description = "Generate Jacoco coverage reports for the test build."

    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }

    def excludes = [
            '**/*Test*.*',
            '**/actions/*.*',
    ]

    def javaClasses = fileTree(dir: "${buildDir}/classes/java/main", excludes: excludes)
    classDirectories.from = files([javaClasses])

    sourceDirectories.from = files([
            "$project.projectDir/src/main/java",
    ])

    executionData.from = files("${project.buildDir}/jacoco/test.exec")
}
