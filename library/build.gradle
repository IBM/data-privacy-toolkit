/*******************************************************************
 *                                                                 *
 * Copyright IBM Corp. 2022                                        *
 *                                                                 *
 *******************************************************************/
apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

ext {
    junit_jupiter_version = '5.9.2'
    junit_platform_launcher_version = '1.9.2'

    mockito_version = '5.2.0'
    jdefault_version = '2.0.0'
    hamcrest_version = '2.2'

    jackson_version = '2.14.2'
    jackson_dataformat_version = '2.14.2'

    log4j2_version = '2.20.0'

    commons_codec_version = '1.13'
    commons_csv_version = '1.10.0'
    commons_io_version = '2.7'
    commons_lang_version = '3.12.0'
    commons_math_version = '3.6.1'
    commons_text_version = '1.8'
    commons_validator_version = '1.7'
    pdfbox_version = '2.0.27'

    poi_version = "5.2.3"

    lucene_version = "6.5.0"
    open_nlp_version = "2.1.1"
}

dependencies {
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:${jackson_dataformat_version}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jackson_dataformat_version}"

    implementation "org.apache.commons:commons-lang3:${commons_lang_version}"
    implementation "org.apache.commons:commons-csv:${commons_csv_version}"
    implementation "org.apache.commons:commons-math3:${commons_math_version}"
    implementation "commons-validator:commons-validator:${commons_validator_version}"

    implementation "org.apache.logging.log4j:log4j-api:${log4j2_version}"
    implementation "org.apache.poi:poi-ooxml:${poi_version}"
    implementation "org.apache.pdfbox:pdfbox:${pdfbox_version}"

    implementation "ca.uhn.hapi:hapi-base:2.3"
    implementation "ca.uhn.hapi:hapi-structures-v22:2.3"
    implementation "ca.uhn.hapi:hapi-structures-v23:2.3"
    implementation 'org.iban4j:iban4j:3.2.4-RELEASE'

    implementation "org.apache.lucene:lucene-analyzers-common:${lucene_version}"
    implementation "org.apache.opennlp:opennlp-tools:${open_nlp_version}"
    testImplementation "org.apache.logging.log4j:log4j-core:${log4j2_version}"
    testImplementation "org.junit.jupiter:junit-jupiter:${junit_jupiter_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_jupiter_version}"
    testImplementation "org.mockito:mockito-core:${mockito_version}"
    testImplementation "org.hamcrest:hamcrest:${hamcrest_version}"
    testImplementation "org.beer30:jdefault:${jdefault_version}"

    testRuntimeOnly "org.junit.platform:junit-platform-launcher:${junit_platform_launcher_version}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit_jupiter_version}"
}

sourceCompatibility = '11'
targetCompatibility = '11'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
    minHeapSize = "5g"
    maxHeapSize = "10g"

    testLogging {
        events 'failed'
        exceptionFormat 'short'

        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams false
    }
}

group = 'com.ibm.research.drl.dpt'
version = '6.0.0'

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        url = uri('https://www.dcm4che.org/maven2')
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = "sources"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Data Privacy Toolkit'
                description = 'The IBM Data Privacy Toolkit (DPT)'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}

tasks.create(name: "testCoverage", type: JacocoReport, dependsOn: "test") {

    group = "Reporting"
    description = "Generate Jacoco coverage reports for the test build."

    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }

    def excludes = [
            '**/*Test*.*',
            '**/actions/*.*',
    ]

    def javaClasses = fileTree(dir: "${buildDir}/classes/java/main", excludes: excludes)
    classDirectories.from = files([javaClasses])

    sourceDirectories.from = files([
            "$project.projectDir/src/main/java",
    ])

    executionData.from = files("${project.buildDir}/jacoco/test.exec")
}
